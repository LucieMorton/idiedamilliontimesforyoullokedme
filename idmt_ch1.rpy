label idmt_ch1: 
    $ save_name = "Я умерла... Прилог 'Ночь гърянет'"
    play ambience idmt_vinyl 
    show layer master at idmt_blur: 
        mesh True shader "witchhouse.oldcinema" 
        u_resolution (1920, 1080)    
        u_mouse (960, 540, 0)
    show text ("{font=IDMT/fonts/LC Chalk.ttf}{size=55}MyLastWitchout\nпредставляет{/size}{/font}") zorder 1 at truecenter with dspr: 
        idmt_dizzy(2.5, 2.5) 
    $ renpy.pause (15.0, hard=True) 
    hide text  
    show text ("{font=IDMT/fonts/LC Chalk.ttf}{size=55}Модификацию на игру\nБесконечное Лето{/size}{/font}") zorder 1 at truecenter: 
        idmt_dizzy(2.5, 2.5) 
    with dspr
    $ renpy.pause (15.0, hard=True) 
    hide text 
    show text ("{font=IDMT/fonts/LC Chalk.ttf}{size=85}Я умерла миллион раз,\nчтобы ты заметил меня:\nRedux{/size}{/font}") zorder 1 at truecenter: 
        idmt_dizzy(2.5, 2.5) 
    with dspr
    $ renpy.pause (15.0, hard=True) 
    hide text with dspr 
    show layer master 
    stop ambience 
    play sound idmt_bell 
    $ renpy.pause (18.0, hard=True)  
    play ambience idmt_vinyl 
    show layer master at idmt_blur: 
        mesh True shader "witchhouse.oldcinema" 
        u_resolution (1920, 1080)    
        u_mouse (960, 540, 0)
    show text ("{font=IDMT/fonts/LC Chalk.ttf}{size=55}Прилог\n\n\n'Ночь гърянет'{/size}{/font}") zorder 1 at truecenter with dspr: 
        idmt_dizzy(2.5, 2.5) 
    $ renpy.pause (15.0, hard=True) 
    hide text with dspr 
    show layer master 
    play ambience ambience_int_cabin_night fadein 1.0
    play music music_list["trapped_in_dreams"]
    scene idmt_int_house_of_mt_night with fade 
    $ renpy.pause (1.5, hard=True)
    window show 
    "Моя последняя ночь перед отъездом из лагеря." 
    "За одну эту неделю со мной произошло, наверное, больше, чем за всю мою жизнь.{w} Немудрено, что пролетела она так быстро." 
    th "Разумеется, неделя пролетела, а не жизнь."
    "Желание отыскать хоть какие-то ответы больше не мучило меня." 
    "Вопросы, конечно, оставались.{w} Например, куда бы я попал, сев на автобус?" 
    th "Ну да там было бы видно.{w} Довольно пофигистично, зато правда." 
    "Немного жаль было уезжать, зная, что отношений за смену я так ни с кем и не выстроил." 
    th "{i}Или выстроил?{/i}" 
    "..." 
    "Странный вопрос.{w} Наверное, так и должно быть, когда просыпаешься посреди ночи и твои мысли в полном беспорядке?" 
    "..."
    th "{i}Конечно же, нет.{/i}{w} Так быть не должно." 
    "..."
    "Ладно." 
    "Скорее всего, я просто в очередной раз в жизни упустил свою возможность.{w} Очевидно же." 
    th "Если я ничего не помнил, значит, и помнить было не о чем."
    "Ночь...{w} Который сейчас час?" 
    "Я медленно приподнялся.{w} По моей руке от кисти до локтя пронеслось несколько горячих неприятных волн." 
    th "{i}Отлежал, наверное.{/i}" 
    "Подождав, когда зуд утихнет, я подошел к окну." 
    window hide 
    camera: 
        perspective True 
        xpos 0 ypos 0 zpos 0 
        ease 3.5 xpos 150 ypos -30 zpos -700
    $ renpy.pause (4, hard=True) 
    play sound idmt_curtains
    scene idmt_int_house_of_mt_night2 with dspr 
    $ renpy.pause (2.5, hard=True) 
    scene idmt_sky  
    show idmt_moon zorder 1 
    camera: 
        perspective True 
        xpos 0 ypos 0 zpos 0
    with dspr 
    $ renpy.pause (1.5, hard=True) 
    window show 
    "Полная луна стояла высоко над безмолвным лагерем." 
    th "Поздно.{w} Очень поздно." 
    window hide 
    $ renpy.pause (0.5, hard=True) 
    scene idmt_int_house_of_mt_night2 with dspr 
    $ renpy.pause (0.5, hard=True) 
    window show 
    "Обычно в эту пору я видел уже третий сон. Но сейчас..." 
    "Сна больше не чувствовалось ни в одном глазу." 
    "Я кинул взгляд на соседнюю кровать." 
    "Она была пуста." 
    stop music fadeout 2.5 
    "Я напрасно пытался вспомнить, в домике ли этой ночью легла спать {i}вожатая{/i}." 
    "Была ли она вообще днем в лагере?{w} Может быть, уехала по делам?" 
    "Это могло бы объяснить ее отсутствие.{w} {i}Если бы мне только вспомнить...{/i}"
    th "Не могла же она уехать посреди ночи?" 
    "..." 
    play sound_loop sfx_head_heartbeat fadein 15.5 
    "Вторая подряд внезапная забывчивость за ночь даром не прошла." 
    "Ход времени замедлился - вернейший признак надвигавшейся паники." 
    "Словно у некоей застарелой психической нестабильности случился рецидив."
    "Ну да, можно с полным на то правом назвать меня психом.{w} И это будет чистая правда."
    "Просто чуть ли не всю сознательную жизнь я очень боялся оставаться один в темноте." 
    scene idmt_int_house_of_mt_night2: 
        matrixcolor SaturationMatrix(1.0) * BrightnessMatrix(0)
        linear 111 matrixcolor SaturationMatrix(0.1) * BrightnessMatrix(-0.1)
    $ witchhouse_shake = 0.5 
    show layer master at idmt_mshake: 
        subpixel True
        align (0.5, 0.5) zoom 1.0
        linear 666 align (0.5, 0.2) zoom 2.0 
    "Я мог не помнить, куда подевалась моя соседка, с кем из пионерок я сдружился за смену и сдружился ли вообще...{w} Но именно это и именно сейчас я помнил превосходно." 
    "C возрастом я почти что преодолел эту фобию.{w} Наличие интернета под рукой создавало ощущение чьего-то ободряющего присутствия, а спать я ложился зачастую с первыми рассветными лучами." 
    "А сейчас же интернета не было.{w} И до рассвета, как бы рано он тут ни наступал, ещё довольно долго." 
    "..." 
    $ witchhouse_shake = 1 
    th "Выходило, что спать она легла всё-таки в домике." 
    "И...{w=0.666} всё."
    "..."
    "Тик-так... тик-так..." 
    "...тик-так... тик-так..."
    "Разум опустел.{w} Я поймал себя на том, что отсчитываю, сколько раз протикали часы на окне."
    "..."
    "Шестьсот шестьдесят четыре..." 
    "...шестьсот шестьдесят пять..."
    $ witchhouse_shake = 1.5 
    "Ситуация всё сильнее и сильнее стала напоминать дешёвый хоррор." 
    "Я начал измерять шагами домик, стараясь и одновременно боясь создать хоть какой-нибудь шум." 
    "Мне нужно хотя бы чем-то себя отвлечь." 
    th "Может, попробовать исследовать, кхм, женскую половину комнаты, пока никто не мешает?" 
    "Но зачем?"
    "Хотя...{w} Всё же было любопытно иногда, какие секреты могли там храниться." 
    show layer master 
    with dspr
    stop sound_loop 
    "Я подошёл к пустовавшей кровати вожатой{w=3.5}{nw}" 
    window hide
    camera: 
        perspective True 
        xpos 0 ypos 0 zpos 0 
        ease 3.5 xpos 250 ypos -50 zpos -550 
    $ renpy.pause (3.5, hard=True)
    play sound sfx_blanket_off_stand 
    show idmt_white: 
        alpha 0.2 
        ease 0.1 alpha 0 
    window show
    extend " и дёрнул покрывало на себя." 
    "В воздух поднялись тучи пыли." 
    play sound_loop sfx_head_heartbeat 
    $ witchhouse_shake = 2.5
    show layer master at idmt_mshake
    me "Какого..."
    "Я начал ошалело открывать все ящики и шкафчики, не заботясь о пионерском приличии." 
    "Мне попадались лишь какие-то бумаги, ничуть не менее пыльные, чем кровать." 
    "Как если бы ничто из этого никогда никем не использовалось по назначению."
    th "Но ведь она спала в этой кровати и перебирала эти бумаги при каждом удобном случае?" 
    "Хотя я уже и не был уверен ни в чём." 
    window hide 
    play sound sfx_paper_bag
    camera: 
        perspective True 
        xpos 250 ypos -50 zpos -550 
        ease 3.5 xpos 150 ypos -30 zpos -700 
    $ renpy.pause (0.35, hard=True)
    window show 
    "Выудив из последнего обысканного шкафчика папку с надписью 'Личное дело', я подошёл с находкой к окну." 
    "В лунном свете стало ясно, что {i}листы были девственно чистыми{/i}." 
    "{i}Что-то происходило.{w} Что-то, отчаянно требовавшее моего внимания.{w} Что-то, чего не должно было произойти даже здесь.{/i}" 
    "..."
    "Уснуть я уже и не надеялся." 
    "А покидать жилище, выходя в неизвестность, могло быть опасно." 
    "Не было никакой гарантии того, что нечто странное творилось только в этом домике." 
    "Тут со мной хотя бы не случилось ничего такого.{w} {i}Пока что.{/i}"
    "..." 
    window hide 
    $ renpy.pause (2.5, hard=True)
    camera: 
        perspective True 
        xpos 150 ypos -30 zpos -700 
        ease 3.5 xpos 0 ypos 0 zpos -15 
    $ renpy.pause (3.5, hard=True) 
    window show
    "В последний раз нервно оглядевшись, я отшвырнул папку и шагнул на выход." 
    "Бумаги разлетелись во все стороны."
    play sound sfx_open_door_1
    "Меня немного успокоил тот факт, что дверь не издала низкий долгий скрип, чтобы уж по всем канонам." 
    th "Чем дешевее хоррор выглядит со стороны, тем хреновее понять, что ты оказался непосредственно внутри него." 
    window hide 
    $ renpy.pause (1.5, hard=True) 
    stop sound_loop 
    scene bg ext_house_of_mt_day at idmt_night_filter 
    show idmt_flashlight zorder 1
    show expression(CustomParticles("images/anim/snow.png", 50)) zorder 2 
    camera: 
        perspective True 
        xpos 0 ypos 0 zpos 0
    with dissolve  
    play ambience ambience_camp_center_night 
    $ renpy.pause (1.5, hard=True) 
    window show 
    "Я выскочил из домика и запер дверь за собой." 
    th "Достать бы доски и заколотить двери и окна." 
    "..."
    "Поначалу на улице мне стало...{w} куда спокойнее." 
    "Ночной ветерок довольно быстро остудил мою вздумавшую чудить голову." 
    "Идти в такую пору никуда не хотелось, и я уселся в скрипнувший шезлонг, намереваясь дожидаться утра здесь." 
    th "Шезлонг - это другое.{w} Он и должен скрипеть.{w} Вот так вот, уютно."
    "Меня легко покачивало туда-сюда." 
    "Я просто сидел и смотрел на фонарь над дверью.{w} К его дрожавшему свету слеталось множество мелких бабочек." 
    "Обычная, спокойная ночь." 
    me "Всем, кто ложится спать, спокойного сна." 
    "Пропел я про себя." 
    "..."
    th "Интересно, есть ли ещё в Совёнке совы?" 
    th "Ну, одна-то в лесу точно имелась, кажется...{w} Я не про птиц, но интересно, как она там."
    th "Сидит, наверное, на тёмных ветвях, кричит так дико и жалобно: кы-ысь! кы-ысь! – а видеть её никто не может." 
    th "Пойдёт пионер так вот ночью или днём в лес, а она ему на шею-то сзади: хоп! и хребтину зубами: хрусь!" 
    th "А когтём главную-то жилочку нащупает и перервёт, и весь разум из пионера и выйдет." 
    th "Мда, это уже не про сов. Это...{w=1}{nw}"
    window hide 
    $ renpy.pause (1.5, hard=True) 
    scene idmt_white 
    play ambience idmt_vinyl 
    show layer master at idmt_blur: 
        mesh True shader "witchhouse.oldcinema" 
        u_resolution (1920, 1080)    
        u_mouse (960, 540, 0)
    show text ("{font=IDMT/fonts/LC Chalk.ttf}{size=55}{color=#000000}Есть у совы голова,\nК ней прикреплён клюв,\nУшей у совы два,\nПоэтому чуткий слух.{/color}{/size}{/font}") zorder 1 at truecenter with dspr: 
        idmt_dizzy(2.5, 2.5) 
    $ renpy.pause (15.0, hard=True) 
    show layer master at idmt_oldcinema
    show cg d2_sovenok zorder 2: 
        blur 3.5 
    play music idmt_lwo 
    $ renpy.pause (6.5, hard=True)
    stop music 
    scene bg ext_house_of_mt_day at idmt_night_filter 
    show idmt_flashlight zorder 1
    show expression(CustomParticles("images/anim/snow.png", 50)) zorder 2 
    play ambience ambience_camp_center_night 
    $ renpy.pause (1.5, hard=True)  
    play music idmt_night 
    window show 
    "..." 
    "Твою мать."
    "Что за нахрен?" 
    "{i}Это{/i} было настолько реалистично, что я даже почувствовал тяжесть в плечах, как если бы на мне действительно кто-то повис." 
    "А я ведь только-только успокоился."
    "Исчезновение вожатой, провалы в памяти, {i}видения{/i}..."
    "Слишком много для меня одного." 
    th "Даже в доселе пытавшемся казаться нормальным мире Совёнка испытывать галлюцинации - плохой признак."
    "..." 
    "Если уж и бояться, то только в компании."
    "А сейчас мне не с кем было разделить страх, уже и так выгнавший меня из домика.{w} Значит..." 
    "Нужно было просто найти {i}кого-нибудь{/i}.{w} И как я сразу не догадался..." 
    "Наверняка у кого-то сегодня тоже была бессонная ночь." 
    "Завтра отъезд.{w} Вряд ли все эти люди когда-нибудь ещё встретятся друг с другом." 
    th "Ко мне это тоже относилось, кстати." 
    "О бегстве из собственного домика, наверное, лучше будет молчать."
    "Встав с шезлонга, я вдохнул полной грудью ставший словно картонным воздух и отправился по дорожке в сторону домиков." 
    th "Кто ходит в гости по ночам - тот поступает... как-то там."
    "..." 
    play sound sfx_owl_far
    dreamgirl "Кы-ысь... кы-ысь..."
    window hide 
    $ renpy.pause (1.5, hard=True)
    scene bg ext_houses_day at idmt_night_filter: 
        zoom 1.10 anchor (48, 27)
        ease 0.30 pos (0,0)
        ease 0.30 pos (25, 25)
        ease 0.30 pos (0, 0)
        ease 0.30 pos (-25, 25)
        repeat 
    with dissolve 
    $ renpy.pause (5.5, hard=True) 
    scene bg ext_house_of_sl_day at idmt_night_filter with dissolve 
    $ renpy.pause (1.5, hard=True) 
    window show 
    "Первым меня не особо приветливо встретил, внезапно вынырнув из темноты, домик {i}активистки{/i} и {i}сони{/i}." 
    "Я неловко начал топтаться у его порога и что-то бурчать себе под нос." 
    "Очевидная мысль, что нужно, например, постучаться, далеко не сразу пришла мне на ум." 
    "Я неуверенно протянул руку к двери и{nw}"
    play sound sfx_knocking_door_2
    extend " постучал несколько раз." 
    "Ответа не последовало." 
    "..."
    th "Может, намаявшись за день, активистка спала без задних ног." 
    th "Вряд ли за прошедшую неделю я досконально изучил характер девушки." 
    th "И кто знает, может быть, мне крупно повезло, что я по ведомой лишь одному черту причине не нарушил её сон." 
    "..."
    th "Для библиотекарши же, думаю, и двадцать четыре часа в сутки проспать не проблема." 
    th "Мне вдвойне крупно повезло, что я и её сон не нарушил."
    "..." 
    "Приложив ухо к двери, я не услышал ни сонного сопения девчонок, ни шороха, ни скрипа кроватей." 
    "Сквозь щели в нос ударил едва уловимый запах затхлости и гнили."
    "Я почувствовал прошедший по коже холодок, не имевший к ночной прохладе никакого отношения." 
    play sound sfx_knock_door_closed_hard2 
    "Я заколотил в дверь изо всех сил." with vpunch
    "Еще чуть-чуть - и я бы её вышиб." 
    play sound sfx_knock_door_closed_hard2
    "Эта мысль и остановила меня как раз в тот момент,{nw}"
    play sound sfx_lock_click
    extend " когда в замке что-то щёлкнуло." 
    "Не ключ, нет." 
    "Больше ничего не происходило."
    "Я резко развернулся к избушке задом, а к дороге передом." 
    scene bg ext_houses_day at idmt_night_filter: 
        zoom 1.10 anchor (48, 27)
        ease 0.30 pos (0,0)
        ease 0.30 pos (25, 25)
        ease 0.30 pos (0, 0)
        ease 0.30 pos (-25, 25)
        repeat 
    with dissolve 
    play sound sfx_owl_far
    "Лишь дикое уханье разрывало гнетущую ночную тишь." 
    "Кроме него, я не слышал ни единого звука, даже шума собственных шагов."
    "Неприятные подвижные думы роились в моей голове." 
    scene bg ext_house_of_dv_day at idmt_night_filter with dissolve 
    "Зачем-то сделав огромный крюк, я набрел на жильё {i}рыжух{/i}." 
    "Оно и днём было тем местом, куда человеку, лично знавшему этих оторв, вряд ли захотелось бы совать нос." 
    "А ночью же..."
    "С другой стороны, {i}этой{/i} ночью возможность подвергнуться физической боли и унижению пугала гораздо меньше, чем кое-что другое." 
    "Первым, что меня категорически не обрадовало при приближении к домику, была {i}непроглядная{/i} тьма в его окнах." 
    "Вариант, что девчонки просто могли спать, я не рассматривал."
    "Я завернул за угол, укрываясь от взгляда пустых глазниц Весёлого Роджера, и зачем-то обошёл домик кругом." 
    "Зуд в обожжённых крапивой голенях добавился к вновь проснувшемуся зуду в руке." 
    "Тьма внутри домика не собиралась выдавать мне своих страшных тайн."
    "Стучать в дверь в этот раз я не рискнул." 
    "Мизерные остатки самообладания, пока что не дававшие мне завизжать фальцетом на всю округу, потихоньку улетучивались." 
    scene bg ext_houses_day at idmt_night_filter 
    with dissolve
    "Вновь выйдя на дорогу, я дико заозирался по сторонам." 
    "Мой слух обострился так, что приложив ухо к стволу дерева, я мог бы с уверенностью ответить на вопрос, существует ли райцентр или нет, если бы захотел." 
    "Единственным источником света вокруг была полная луна над лагерем." 
    "Фонарь над нашим с вожатой домиком успел погаснуть."
    "Я бы не удивился, если бы луна в этот момент была повёрнута к Совёнку темной стороной." 
    "Как и сам лагерь ко мне.{w} Как будто ему мало того, что он мне так по-настоящему и не открылся."
    play sound sfx_owl_far
    dreamgirl "Кы-ысь... кы-ысь..." 
    th "Вот привязалось ко мне..."
    "Понятное дело, эти звуки вообще не радовали." 
    "Где-то, как шаровая молния, таилась какая-то опасность, реагировавшая на все мои действия и неумолимо близившаяся." 
    "Я отверг мысль пойти на сцену, где ночью иногда можно было бы отыскать одну из рыжих." 
    "Во-первых, пришлось бы пройти мимо моего домика, где вся эта чертовщина и началась."
    "Кстати, что я все {i}рыжие{/i} да {i}рыжие{/i}... У них ведь и {i}имена{/i} были." 
    "{i}Были{/i}..." 
    th "Имена... Ахаха.{w} {i}Можно подумать, имя тут что-то значило.{/i}" 
    "Такие выводы и на фоне происходящего умудрялись выбиться из всех прочих; они были настолько чуждыми, что я ненадолго почувствовал боль, пытаясь изгнать их из головы." 
    th "Нет.{w} В имени огромная сила.{w} Имя определяет сущность." 
    th "И если имя теряет смысл, то что тогда происходит с его хозяином?" 
    "..."
    th "Это, возможно, крайне интересная тема для рассуждений, но не здесь, не сейчас и не для меня."
    "..." 
    play sound sfx_owl_far
    dreamgirl "Кы-ысь... кы-ысь..." 
    "Мои зубы вовсю ритмично стучали." with vpunch
    th "Зря я не надел пальто.{w} Знал же, что на улице холодно." 
    th "Ну как знал...{w} Догадывался." 
    th "Проверю, пожалуй, ещё один домик, чтобы успокоить хотя бы свою совесть."
    th "Успокоиться же мне целиком и полностью точно не вышло бы, даже выпей я все успокоительное, хранившееся в медпункте."
    th "Может, не дожидаясь рассвета, удрать вдаль по дороге...{w} Непохоже, что тут осталось кому меня хватиться." 
    th "Бежать, куда угодно, лишь бы подальше отсюда!!!"
    th "Так, ладно.{w} Я вроде как хотел домик осмотреть." 
    th "Был ли в этом смысл?{w} По сути, я несколькими минутами ранее уже подвел итоги для себя. Но..."
    scene bg ext_houses_day at idmt_night_filter: 
        zoom 1.10 anchor (48, 27)
        ease 0.30 pos (0,0)
        ease 0.30 pos (25, 25)
        ease 0.30 pos (0, 0)
        ease 0.30 pos (-25, 25)
        repeat 
    with dspr
    "Кое-как сдвинувшись с места, я медленнее улитки пополз вперёд." 
    "Мне суждено было осознать и кое-что ещё.{w} Не иначе как кто-то решил, что кошмаров этой ночи мне было недостаточно."
    "Я {i}помнил{/i} лишь то, что {i}завтра должен быть разъезд{/i}." 
    "Я помнил, как сюда попал, помнил некоторые основные локации, некоторые события без привязки к чему-либо, очень расплывчато мог описать основные черты лиц и характеров..." 
    "Ну как {i}помнил{/i}...{w} Это больше походило на растянувшийся бэд трип, в ходе которого пережитые негативные опыты, забытые и погребённые глубоко в пыли подсознания, с новой силой напоминали о себе."
    "Вдобавок к именам местных жителей, я почти абсолютно, за исключением пары вещей, забыл то, что происходило предыдущие шесть дней." 
    "Всё то, чем я жил, дышал, дорожил, оказалось всего лишь условностью, ждавшей подходящего момента, чтобы предательски осыпаться как карточный домик и оставить меня наедине с моими демонами." 
    "..." 
    stop music 
    "{cps=10}Ещё издали я разглядел,{w=1}{nw}{/cps}" 
    stop ambience
    scene expression(idmt_darkpict("bg int_house_of_un_day")): 
        truecenter 
        zoom 0.47 
    show idmt_ext_house_of_un_night_without_doors 
    play music idmt_doolb 
    $ renpy.music.set_volume(0.2, delay=0.001, channel="music") 
    extend "{cps=10} что в домике {i}музыкантки{/i} и {i}Лены{/i} дверь была как будто выбита с петель.{/cps}" 
    stop music 
    scene bg ext_houses_day at idmt_night_filter 
    play ambience ambience_camp_center_night 
    "Я все же был {i}не настолько тупой{/i}." 
    "Имя девочки из четырёх букв показалось самым осмысленным словом в мире.{w} Но как мне это могло помочь?"
    "Мы поменялись ролями, и теперь уже бездна мглы внутри домика, обладавшая собственной злой волей и разумом, внимательно вглядывалась в меня." 
    window hide
    stop ambience 
    scene expression(idmt_darkpict("bg int_house_of_un_day")): 
        truecenter 
        zoom 0.47 
    show idmt_ext_house_of_un_night_without_doors zorder 1
    play music idmt_doolb 
    $ renpy.music.set_volume(0.4, delay=0.001, channel="music") 
    $ witchhouse_shake = 2.5
    show layer master at idmt_mshake: 
        truecenter 
        zoom 1 
        ease 4.5 zoom 1.222
    $ renpy.pause (4.5, hard=True) 
    stop music 
    play ambience ambience_camp_center_night 
    scene bg ext_houses_day at idmt_night_filter  
    window show
    "Она взывала ко мне голосами миллиона Лен, простирала ко мне свои призрачные щупальца, пряно пахнувшие гемоглобином." 
    "Меня не могло надолго хватить.{w} Слабое сопротивление было быстро сломлено." 
    "Тело двинулось с места на этот зов, на манер зомби вытянув руки вперёд." 
    window hide
    stop ambience 
    scene expression(idmt_darkpict("bg int_house_of_un_day")): 
        truecenter 
        zoom 0.47 
    show idmt_ext_house_of_un_night_without_doors zorder 1
    play music idmt_doolb 
    $ renpy.music.set_volume(1.0, delay=0.001, channel="music") 
    $ witchhouse_shake = 6.66
    show layer master at idmt_mshake: 
        truecenter 
        zoom 1.666 
        ease 6.66 zoom 3 
    show idmt_black zorder 2: 
        alpha 0.0 
        ease 6.66 alpha 1.0
    $ renpy.pause (6.66, hard=True) 
    stop music fadeout 0.5
    $ renpy.pause (6.66, hard=True) 
    window show 
    "..." 
    "Я зашёл в домик." 
    "Я просто в него зашёл.{w} Никто на меня не набросился, не ударил, не проткнул мне ножом глотку.{w} Ничего." 
    "И в последний момент меня это...{w} даже как-то разочаровало." 
    scene expression(idmt_darkpict("bg int_house_of_un_day")) 
    show unblink zorder 1
    me "Что же ты мне расскажешь, Лена..." 
    me "Не поверишь, мне вдруг столько всего захотелось у тебя спросить." 
    me "Только ты это, не томи." 
    me "А то мне это… *зевок* спать охота..." 
    "И правда, прерванный сон, о котором всего мгновение назад я и не думал, снова напомнил о себе." 
    "Хотелось повалиться прямо сейчас, хоть на голый пол, что я бы и сделал, не будь это место таким несимпатичным." 
    th "В конце-то концов, я просто-напросто зачем-то проник в чужое жилище." 
    th "Мало ли зачем дверь открыли.{w} Для меня, что ли."  
    "..."
    th "Экий вы наивный, Семён." 
    "Лена молчала.{w} Была ли она здесь..." 
    "{i}Существовала ли она вообще{/i}..." 
    "Я скосил глаза вбок{nw}" 
    window hide
    show idmt_un_suicide zorder 2: 
        matrixcolor BrightnessMatrix(0.4) * ContrastMatrix(2.5) * ColorizeMatrix("#000000", "#ff0000")
    play sound idmt_glitch 
    $ renpy.pause (0.01666) 
    stop sound 
    hide idmt_un_suicide
    camera: 
        perspective True 
        xpos 0 ypos 0 zpos 0 
        ease 3.5 xpos -250 ypos -50 zpos -550 
    show layer master: 
        blur 0 
        ease 2 blur 10 
    show expression(idmt_burneyespict("bg int_house_of_un_day")) with dissolve: 
        alpha 0.1 
        idmt_dizzy (2.5, 2.5) 
    play sound_loop sfx_head_heartbeat fadein 45.5 
    window show
    extend " и не успел ни различить очертаний, ни понять, что я увидел."
    "Сетчатку будто обожгло." 
    "Сила земного притяжения возросла во много раз.{w} Боль в руке стала нестерпимой." 
    "Мой рот открылся в беззвучном крике." 
    play sound sfx_bodyfall_1 
    "Я мешком повалился на что-то жесткое." with vpunch 
    show blink onlayer widgetoverlay
    "Во рту почувствовался привкус крови." 
    "..."
    window hide 
    scene idmt_black
    camera: 
        perspective True 
        xpos -250 ypos -50 zpos -550 
        ease 3.5 xpos 0 ypos 0 zpos 0 
    stop sound_loop fadeout 6.66 
    hide blink onlayer widgetoverlay
    $ renpy.pause (6.66, hard=True)
    jump idmt_ch2
